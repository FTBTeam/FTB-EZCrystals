plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev.legacyforge' version '2.0.120'
    id 'idea'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

def mcMinorAndPatch = getMinorAndPatchFromMc()

version = "${mcMinorAndPatch}.${mod_version}"
group = project.mod_group_id

apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/changelog.gradle"
apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/publishing.gradle"

base {
    archivesName = project.archives_base_name
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    runs {
        client {
            client()

            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
    	url "https://maven.saps.dev/minecraft"
    	content {
    		includeGroup "com.shynieke.geore"
    	}
    }

    maven {
    	url "https://cursemaven.com"
    	content {
    		includeGroup "curse.maven"
    	}
    }
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    modCompileOnly "com.shynieke.geore:GeOre-${geore_minecraft}:${geore_version}"
	modRuntimeOnly "com.shynieke.geore:GeOre-${geore_minecraft}:${geore_version}"

	modCompileOnly "curse.maven:applied-energistics-2-223794:7148487"
	modRuntimeOnly "curse.maven:applied-energistics-2-223794:7148487"
	modCompileOnly "curse.maven:guideme-1173950:7127447"
	modRuntimeOnly "curse.maven:guideme-1173950:7127447"
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
            pack_format: pack_format
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    // Set to java 17 compatibility
    options.release.set(17)
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.register("sourcesJar", Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
            artifact sourcesJar
            // Uncomment this if you want to create an API jar
            // artifact apiJar
        }
    }

    repositories {
        if (ftbPublishing.ftbToken) {
            maven {
                url ftbPublishing.ftbURL
                credentials {
                    username = ftbPublishing.ftbUser
                    password = ftbPublishing.ftbToken
                }
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

def getMinorAndPatchFromMc() {
    String mcVersion = project.minecraft_version
    String[] split = mcVersion.split("\\.")
    return split[1] + "." + (split[2] ?: "0")
}

publishMods {
    dryRun = providers.environmentVariable("CURSEFORGE_KEY").getOrNull() == null
    changelog = createChangelog(project)
    version = mod_version

    def tag = providers.environmentVariable("TAG").getOrElse("release")
    type = tag.endsWith("-beta") ? BETA : (tag.endsWith("-alpha") ? ALPHA : STABLE)

    curseforge {
        displayName = "${mod_name} ${mod_version}"
        file = project.provider { tasks.jar }.flatMap { it.archiveFile }
        projectId = curseforge_id
        accessToken = providers.environmentVariable("CURSEFORGE_KEY")
        minecraftVersions.add("${minecraft_version}")
        javaVersions.add(JavaVersion.VERSION_17)
        modLoaders.add("forge")

        optional('geore')
        optional('applied-energistics-2')
    }
}
